//file:noinspection DependencyNotationArgument
// TODO remove when fixed in RFG ^
dependencies {
    // Hard Dependencies

    // the CCL deobf jar uses very old MCP mappings, making it error at runtime in runClient/runServer
    // therefore we manually deobf the regular jar
    implementation rfg.deobf("curse.maven:codechicken-lib-1-8-242818:2779848")

    // Soft Dependencies
    // Can change any of these from compileOnlyApi -> implementation to test them in-game.

    implementation "mezz.jei:jei_1.12.2:4.16.1.302"
    implementation "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.684"
    implementation rfg.deobf("curse.maven:top-245211:2667280") // TOP 1.4.28
    implementation rfg.deobf("curse.maven:ctm-267602:2915363") // CTM 1.0.2.31

    // use a local jar for GroovyScript to avoid a bug from Mixin and ForgeGradle,
    // causing crashes at run-time when deobfuscated
    implementation files("libs/groovyscript-0.4.0.jar")
    //implementation fg.deobf("curse.maven:groovyscript-687577:4426112") // GRS 0.3.0

    compileOnlyApi rfg.deobf("curse.maven:ae2-extended-life-570458:4402048") // AE2UEL 0.55.6
    // ae2 is a transitive dependency for MTE, so it's also needed at test-time
    testImplementation rfg.deobf("curse.maven:ae2-extended-life-570458:4402048")

    compileOnlyApi rfg.deobf("curse.maven:opencomputers-223008:4526246") // OpenComputers 1.8.0+9833087
    compileOnlyApi "curse.maven:journeymap-32274:2916002" // Journeymap 5.7.1
    compileOnlyApi "curse.maven:voxelmap-225179:3029445" // VoxelMap 1.9.28
    compileOnlyApi "curse.maven:xaeros-263420:4516832" // Xaero's Minimap 23.4.1
}
