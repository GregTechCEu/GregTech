//file:noinspection DependencyNotationArgument
// TODO remove when fixed in RFG ^
/*
 * Add your dependencies here. Common configurations:
 *  - implementation("group:name:version:classifier"): if you need this for internal implementation details of the mod.
 *       Available at compiletime and runtime for your environment.
 *
 *  - compileOnlyApi("g:n:v:c"): if you need this for internal implementation details of the mod.
 *       Available at compiletime but not runtime for your environment.
 *
 *  - annotationProcessor("g:n:v:c"): mostly for java compiler plugins, if you know you need this, use it, otherwise don't worry
 *
 *  - testCONFIG("g:n:v:c"): replace CONFIG by one of the above, same as above but for the test sources instead of main
 *
 * You can exclude transitive dependencies (dependencies of the chosen dependency) by appending { transitive = false } if needed.
 *
 * To add a mod with CurseMaven, replace '("g:n:v:c")' in the above with 'rfg.deobf("curse.maven:project_slug-project_id:file_id")'
 * Example: implementation rfg.deobf("curse.maven:gregtech-ce-unofficial-557242:4527757")
 *
 * For more details, see https://docs.gradle.org/8.0.1/userguide/java_library_plugin.html#sec:java_library_configurations_graph
 */
dependencies {
    // Hard Dependencies

    // the CCL deobf jar uses very old MCP mappings, making it error at runtime in runClient/runServer
    // therefore we manually deobf the regular jar
    implementation rfg.deobf("curse.maven:codechicken-lib-1-8-242818:2779848") // CCL 3.2.3.358

    // Soft Dependencies
    // Can change any of these from compileOnlyApi -> implementation to test them in-game.

    implementation "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.20.684"
    implementation rfg.deobf("curse.maven:ctm-267602:2915363") // CTM 1.0.2.31

    // use a local jar for GroovyScript to avoid a bug from Mixin and ForgeGradle,
    // causing crashes at run-time when deobfuscated
    implementation files("libs/groovyscript-0.4.0.jar")
    //implementation rfg.deobf("curse.maven:groovyscript-687577:4487379") // GRS 0.4.6

    compileOnlyApi rfg.deobf("curse.maven:ae2-extended-life-570458:4402048") // AE2UEL 0.55.6
    // ae2 is a transitive dependency for MTE, so it's also needed at test-time
    testImplementation rfg.deobf("curse.maven:ae2-extended-life-570458:4402048")

    compileOnlyApi rfg.deobf("curse.maven:opencomputers-223008:4526246") // OpenComputers 1.8.0+9833087
    compileOnlyApi "curse.maven:journeymap-32274:2916002" // Journeymap 5.7.1
    compileOnlyApi "curse.maven:voxelmap-225179:3029445" // VoxelMap 1.9.28
    compileOnlyApi "curse.maven:xaeros-263420:4516832" // Xaero's Minimap 23.4.1
}

minecraft {
    injectedTags.put('DEP_VERSION_STRING', "required-after:gregtech@[${modVersion},);")
}

configurations {
    compileClassPath {
        // exclude GNU trove, FastUtil is superior and still updated
        exclude group: "net.sf.trove4j", module: "trove4j"
    }
}
